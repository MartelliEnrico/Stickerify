plugins {
    alias(libs.plugins.graalvm)
    id 'application'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.imgscalr
    implementation libs.telegrambots
    implementation libs.tika
    implementation libs.bundles.logback
}

group = 'com.cellar'
version = '1.0-SNAPSHOT'
description = 'Telegram bot to convert images in the format required to be used as Telegram stickers'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
}

tasks.withType(JavaCompile).configureEach {
    // GraalVM 22.2 supports up to Java 17
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.cellar.stickerify.runner.Main'
}

graalvmNative {
    binaries.configureEach {
        resources.includedPatterns.add('logback.xml')
        buildArgs.addAll('--static', '--libc=musl')
        // Reflection workarounds
        //buildArgs.add('--initialize-at-run-time=com.fasterxml.jackson')
        buildArgs.add('--report-unsupported-elements-at-runtime')
        buildArgs.add('--initialize-at-build-time=ch.qos.logback,org.telegram.telegrambots.meta.api.objects')
        buildArgs.add('-H:ReflectionConfigurationFiles=../../../src/main/resources/reflection-config.json')
    }
}
