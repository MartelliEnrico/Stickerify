plugins {
    alias(libs.plugins.graalvm)
    id 'application'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.imgscalr
    implementation libs.telegrambots
    implementation libs.tika
    implementation libs.bundles.logback
}

group = 'com.cellar'
version = '1.0-SNAPSHOT'
description = 'Telegram bot to convert images in the format required to be used as Telegram stickers'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
}

tasks.withType(JavaCompile) {
    // GraalVM 22.2 supports up to Java 17
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.cellar.stickerify.runner.Main'
}

graalvmNative {
    binaries.all {
        resources.includedPatterns.add('logback.xml')
        buildArgs.addAll('--static', '--libc=musl')
        // Reflection workarounds
        buildArgs.add('--initialize-at-build-time=ch.qos.logback')
        buildArgs.add('-H:ReflectionConfigurationFiles=../../../src/main/resources/reflection-config.json')
    }
}
