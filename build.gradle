plugins {
    alias(libs.plugins.graalvm)
    id 'application'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.imgscalr
    implementation libs.telegrambots
    implementation libs.tika
    implementation libs.bundles.logback
}

group = 'com.cellar'
version = '1.0-SNAPSHOT'
description = 'Telegram bot to convert images in the format required to be used as Telegram stickers'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.cellar.stickerify.runner.Main'
}

graalvmNative {
    binaries.configureEach {
        resources.autodetection {
            enabled = true
            restrictToProjectDependencies = false
        }
        buildArgs.addAll('--static', '--libc=musl')
        buildArgs.add('--report-unsupported-elements-at-runtime')
        buildArgs.add('-H:ReflectionConfigurationFiles=../../../reflection-config.json')
        buildArgs.add('-H:JNIConfigurationFiles=../../../jni-config.json')
    }
}
